#!/usr/bin/env fish
# deploy [host-name] [root@<target-host>]:
#  Builds the flake for host-name locally and deploys to target-host

set domain home.arpa
set logfile log.txt

if test -n "$argv[1]"
  set host $argv[1]
end
if test -n "$argv[2]"
  set target $argv[2]
end

if test -z "$host"
  echo "host env or argument required" >&2
  exit 1
end

if test -z "$target"
  set target "root@$host.$domain"
end

# Build flake locally, push to $host.
echo "Building $host and pushing to $target..."
nixos-rebuild \
  --flake ".#$host" \
  --target-host $target \
  --build-host localhost switch
or exit $status

# Log most recent deploy for host w/ git commit.
test -e $logfile; or touch $logfile
set commit (git log -n 1 --format="[%ci] %h %s")
set tmpfile (mktemp)
cp $logfile $tmpfile
grep -v "^$host " $tmpfile > $logfile
printf "%-12s %s\n" $host $commit >> $logfile
rm -f $tmpfile

echo
echo "Deploy of $host --> $target complete"
